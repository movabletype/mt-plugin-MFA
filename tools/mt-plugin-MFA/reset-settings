#!/usr/bin/env perl

use strict;
use warnings;
use Getopt::Long;
use FindBin;
use lib ("$FindBin::Bin/../../lib", "$FindBin::Bin/../../extlib");
use MT::Bootstrap;
use MT;

my $mt = MT->new() or die MT->errstr;

GetOptions(
    'username=s' => \my $username,
    'email=s'    => \my $email,
    'all'        => \my $all,
);

if ( grep({ $_ } ($username, $email, $all)) != 1 ) {
    die <<USAGE
Usage: $0 --username=[username] --email=[email] --all
Specify only one of --username, --email, --all.
USAGE
}

my $iter = $mt->model('author')->load_iter({
    ($username ? (name => $username) : ()),
    ($email ? (email => $email) : ()),
});
my $count = 0;

while (my $user = $iter->()) {
    $mt->run_callbacks('mfa_reset_settings', $mt, {user => $user});
    $count++;
}

print "Reset settings for $count users.\n";
